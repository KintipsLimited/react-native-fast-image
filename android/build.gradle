buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.2.1"
    }
}

apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _excludeAppGlideModule = safeExtGet("excludeAppGlideModule", false)
def _buildToolsVersion = safeExtGet("buildToolsVersion", "31.0.0")
def _compileSdkVersion = safeExtGet("compileSdkVersion", 31)
def _glideVersion = safeExtGet("glideVersion", "4.11.0")
def _minSdkVersion = safeExtGet("minSdkVersion", 21)
def _reactNativeVersion = safeExtGet("reactNative", "+")
def _targetSdkVersion = safeExtGet("targetSdkVersion", 31)

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    sourceSets {
        main {
            java {
                if (_excludeAppGlideModule) {
                    srcDir "src"
                    exclude "**/FastImageGlideModule.java"
                }
            }
        }
    }

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:${_reactNativeVersion}"
    implementation "androidx.core:core:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.annotation:annotation:1.0.0"
    implementation("com.github.bumptech.glide:glide:${_glideVersion}")
    implementation("com.github.bumptech.glide:annotations:${_glideVersion}")
    annotationProcessor 'com.android.support:support-annotations:28.0.0-alpha3'
    annotationProcessor "com.github.bumptech.glide:compiler:${_glideVersion}"
    implementation("com.github.bumptech.glide:okhttp3-integration:${_glideVersion}")
}
